/*
 *
 *  Copyright 2022 Jeluchu
 *
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

android {

    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures.compose = true
    composeOptions.kotlinCompilerExtensionVersion = "1.2.0"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.jeluchu.jchucomponents'

    dokkaHtml.configure {

        outputDirectory.set(file("../docs"))
        moduleName.set("$rootProject.name")
        suppressInheritedMembers.set(true)
        pluginsMapConfiguration.set(
                [
                        "org.jetbrains.dokka.base.DokkaBase": """{
                            "footerMessage": "Made with ❤️ at <b><a href=\\"https://about.jeluchu.com\\">Jéluchu</a></b> © 2022",
                            "customStyleSheets": ["${file("../logo-style.css")}"]
                        }"""
                ]
        )

        dokkaSourceSets {
            named("main") {
                includes.from("../modules.md")
                includeNonPublic.set(true)
            }
        }
    }

}

dependencies {

    // KOTLIN LIBRARY ------------------------------------------------------------------------------
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // JETPACK COMPOSE -----------------------------------------------------------------------------
    implementation 'androidx.compose.ui:ui:1.2.1'
    implementation 'androidx.compose.material:material:1.2.1'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.2.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation 'com.google.android.gms:play-services-base:18.1.0'
    debugImplementation 'androidx.compose.ui:ui-tooling:1.2.1'
    implementation 'androidx.compose.material:material-icons-extended:1.2.1'
    implementation 'androidx.compose.foundation:foundation:1.2.1'
    implementation 'androidx.compose.foundation:foundation-layout:1.2.1'
    implementation 'androidx.compose.animation:animation:1.2.1'
    implementation 'androidx.compose.runtime:runtime:1.2.1'
    implementation 'androidx.compose.runtime:runtime-livedata:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.1'
    implementation 'androidx.navigation:navigation-compose:2.5.1'

    // FIREBASE LIBRARY ----------------------------------------------------------------------------
    implementation("com.google.firebase:firebase-analytics-ktx:21.1.1")

    // ACCOMPANIST GOOGLE LIBRARY ------------------------------------------------------------------
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.25.0'
    implementation 'com.google.accompanist:accompanist-navigation-animation:0.25.0'

    // ANDROIDX LIBRARY ----------------------------------------------------------------------------
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    // GOOGLE LIBRARY ------------------------------------------------------------------------------
    implementation 'com.google.code.gson:gson:2.9.1'

    // SQUAREUP LIBRARY ----------------------------------------------------------------------------
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'

    // THIRD PARTY DEPENDENCIES --------------------------------------------------------------------
    implementation 'io.coil-kt:coil-compose:2.2.0'

}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.jeluchu"
                artifactId = "jchucomponents"
                version = "1.0.0-beta01"
            }
        }
    }
}