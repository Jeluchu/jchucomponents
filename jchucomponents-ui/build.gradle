plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

android {

    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures.compose = true
    composeOptions.kotlinCompilerExtensionVersion = "1.4.7"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions.jvmTarget = JavaVersion.VERSION_17.toString()
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    dokkaHtml.configure {

        outputDirectory.set(file("../docs"))
        moduleName.set("$rootProject.name")
        suppressInheritedMembers.set(true)
        pluginsMapConfiguration.set(
                [
                        "org.jetbrains.dokka.base.DokkaBase": """{
                            "footerMessage": "Made with ❤️ at <b><a href=\\"https://about.jeluchu.com\\">Jéluchu</a></b> © 2022",
                            "customStyleSheets": ["${file("../logo-style.css")}"]
                        }"""
                ]
        )
    }

    namespace 'com.jeluchu.jchucomponents.ui'

}

dependencies {
    implementation libs.bundles.ui.compose
    implementation libs.bundles.ui.accompanist
    implementation libs.bundles.ui.androidx
    implementation project(':jchucomponents-ktx')
    debugImplementation libs.androidx.compose.ui.ui.tooling.preview
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.github.jeluchu"
                artifactId = "jchucomponents-ui"
                version = "1.5.2"
            }
        }
    }
}